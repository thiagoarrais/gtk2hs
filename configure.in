dnl Mogul - a monad based gui library
dnl
dnl Copyright (c) 2001, 2002 Axel Simon <as49@ukc.ac.uk>
dnl with parts stolen from Manuel Chakravaty, Sven Panne and Micheal Weber
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Library General Public
dnl License as published by the Free Software Foundation; either
dnl version 2 of the License, or (at your option) any later version.
dnl 
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Library General Public License for more details.
dnl 
dnl You should have received a copy of the GNU Library General Public
dnl License along with this library (COPYING.LIB); if not, write to the Free
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

dnl ######################################################################
dnl Process this file with autoconf to produce a configure script.
dnl ######################################################################

 
AC_INIT(VERSION)

dnl Check system type.
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(BASENAME,basename)
AC_PATH_PROG(GREP,gnugrep)
AC_PATH_PROG(GREP,ggrep)
AC_PATH_PROG(GREP,grep)
AC_PATH_PROG(GZIP,gzip)
AC_PATH_PROG(SED,gnused)
AC_PATH_PROG(SED,gsed)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(TAR,tar)
AC_PATH_PROG(TOUCH,touch)
dnl On some systems (Solaris,...) asking sh for the current directory
dnl (with a built-in pwd) and asking make for the current directory
dnl gives different results. Thus, if possible, we use the same external
dnl program for determining the current path.
dnl The AC_PATH_PROG macro does not work because pwd is a built-in and
dnl therefore evaluated in the body of the expanded macro.
AC_MSG_CHECKING(for external pwd)
PWD=`which pwd 2> /dev/null`
if test -z "$PWD"; then
  AC_MSG_RESULT([none found])
  PWD="pwd";
else
  AC_MSG_RESULT($PWD)
fi;

dnl Figure out file extensions.
AC_EXEEXT

dnl Check for library pre- and suffixes.
if test x$EXEEXT = x; then
  dnl must be unix
  SLSUFFIX=".a";
  SLPREFIX="lib";
  DLSUFFIX=".so";
  DLPREFIX="lib";
  dnl the hash sign is a comment for sed
  PATHSED=["\#"];
  HSCFLAGS=;
  C2HSFLAGS=" -C-D__signed=signed";
  dnl Where are we? (only used during configuration)
  TOP=`$PWD`;
else
  dnl must be Win32
  SLSUFFIX=".lib";
  SLPREFIX="";
  DLSUFFIX=".dll";
  DLPREFIX="";
  dnl replace something like /c/ with c:/
  PATHSED=["s+/\([a-z]\)/+\1:/+"];
  HSCFLAGS=" -C -optc-fnative-struct";
  C2HSFLAGS=;
  dnl Where are we? (only used during configuration)
  TOP=`$PWD | $SED $PATHSED`;
  WIN32=yes;
fi;



dnl Check for libraries.

dnl Is Manuel's Ports library present?
dnl AC_ARG_WITH(ports, 
dnl	    [  --with-ports=PORTS-CONFIG use this Haskell Ports Library],
dnl	    [PORTS_CONFIG=$withval])


dnl Select a specific Haskell compiler and/or flags.
AC_ARG_WITH(hc, 
            [  --with-hc=HC            use Haskell compiler HC],
	    [HC=$withval])

AC_ARG_WITH(hcflags, 
            [  --with-hcflags=HCFLAGS  flags for Haskell tools, default: -O -H180m],
	    [HCFLAGS=$withval])



dnl Check for GHC-5.02 or greater.
dnl (The next command is not executed, if $HC is already set.)
AC_PATH_PROG(HC, [$HC ghc], ghc-not-found)
if test $HC = ghc-not-found; then
 AC_MSG_ERROR([
Could not find GHC!  This is the only supported compiler.
You need GHC 5.02 upwards.])
fi

GHC=$HC

dnl Check GHC details.
AC_MSG_CHECKING([version of GHC])
GHC_VERSION=`$GHC --version | $SED "s/[[a-zA-Z ,]*\([0-9.]*\)[a-zA-Z ]]*/\1/"`
AC_MSG_RESULT([$GHC_VERSION])

GTKHS_PROG_CHECK_VERSION($GHC_VERSION, -lt, 5.0.2, [
  AC_MSG_ERROR([I need the FFI of GHC 5.02 upwards!])])

dnl Check if the ghc compiler can generate dynamic callbacks with more than
dnl 4 words worth of arguments. Hopefully one day the compiler will support
dnl this.
AC_MSG_CHECKING([broken dynamic callbacks])
FOUR_WORD_CALLBACK=no
GTKHS_PROG_CHECK_VERSION($GHC_VERSION, -lt, 9.9.9, [
  if test $host_cpu = sparc; then FOUR_WORD_CALLBACK=yes; fi
])
AC_MSG_RESULT([$FOUR_WORD_CALLBACK])

dnl Check if this ghc uses the new hierachical module system. If so, we don't
dnl need to depend on the "lang" or "data" package (which don't exist anymore).
AC_MSG_CHECKING([new module system])
NEW_MODULE_SYSTEM=no;
GTKHS_PROG_CHECK_VERSION($GHC_VERSION, -ge, 5.04.0,[NEW_MODULE_SYSTEM=yes;]);
AC_MSG_RESULT([$NEW_MODULE_SYSTEM])

GHCBARE=`basename $GHC`
GHCDIR=`dirname $GHC`
GHCPKGNAME=ghc-pkg`echo $GHCBARE | $SED s/ghc//`

dnl Check for ghc-pkg. Use the one that is in the same directory and
dnl version suffix as the specified compiler.
AC_PATH_PROGS(GHCPKG, $GHCPKGNAME ghc-pkg, ghcpkg-not-found, $GHCDIR)

if test $GHCPKG = ghcpkg-not-found; then
  AC_MSG_ERROR([ghc-pkg not found. (But ghc exists!?)]);
fi

dnl Optimise Haskell by default and give sufficient space.
if test -z "$HCFLAGS"; then
  HCFLAGS=" -O -H180m "
fi


dnl Due to the interdependencies between the different packages we need to
dnl install one package before we can compile the next one. We do this by
dnl using a local package file called localpackage.conf in the toplevel
dnl directory. The user may specify an alternative file and using the complete
dnl suite in-place.
AC_ARG_WITH(pkgconf,
	    [  --with-pkgconf=pkg.conf use another package.conf file for building in-place],
	    [LOCALPKGCONF=$withval],[LOCALPKGCONF=$TOP/localpackage.conf])

dnl Check for pkg-config which holds information about all Gtk related
dnl libraries.
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

if test "$PKG_CONFIG" = "no" ; then
 AC_MSG_ERROR([*** The pkg-config script could not be found. Make sure it is
   *** in your path, or set the PKG_CONFIG environment variable
   *** to the full path to pkg-config.
   *** Or see http://www.freedesktop.org/software/pkgconfig to get pkg-config.
  ])
fi;


dnl Check if user wants GtkGLExt extension. Make the default value for this
dnl flag dependant on whether it is installed or not.
ENABLE_OPENGL=yes;
AC_MSG_CHECKING([if OpenGL extension can be built])

AC_ARG_ENABLE(glext,
	      [  --enable-glext         generate binding for OpenGL rendering],
	      [FOUNDGLEXT=$enableval],[FOUNDGLEXT=yes])

dnl Reset the flag if GtkGLExt is not installed
$PKG_CONFIG --exists gtkglext-1.0 || FOUNDGLEXT=no;
if test x$FOUNDGLEXT = xno; then ENABLE_OPENGL=no;fi;

dnl Reset the flag if HOpenGL is not installed
FOUNDHOPENGL=yes;
$GHCPKG -l | $GREP OpenGL > /dev/null || FOUNDHOPENGL=no;
if test x$FOUNDHOPENGL = xno; then ENABLE_OPENGL=no;fi;

AC_MSG_RESULT($ENABLE_OPENGL)


dnl Check for the GTK&Co libraries. Use the special PKG_CHECK_MODULES
dnl macro from the pkg-config program.
if test x$ENABLE_OPENGL = xno; then
  PKG_CHECK_MODULES(GTK,[glib-2.0 >= 2.0.0 gdk-2.0 >= 2.0.0 gtk+-2.0 >= 2.0.0 gdk-pixbuf-2.0 >= 0.12.0])
else
  PKG_CHECK_MODULES(GTK,[glib-2.0 >= 2.0.0 gdk-2.0 >= 2.0.0 gtk+-2.0 >= 2.0.0 gdk-pixbuf-2.0 >= 0.12.0 gtkglext-1.0 >= 0.7.1])
fi

dnl The configuration program for GTK is kind of stupid in that it
dnl lists directories which don't exist. ghc-pkg in ghc 5.04 or greater
dnl does not like that, so we need to filter out non-existent directories.
TMPGTK_CFLAGS=$GTK_CFLAGS $PIXBUF_CFLAGS;
GTK_CFLAGS=;
for FLAG in $TMPGTK_CFLAGS; do
  case $FLAG in
    -I*) DIR=`echo $FLAG | $SED "s/-I//"`;
	 if test -d $DIR; then GTK_CFLAGS="$GTK_CFLAGS -I$DIR"; fi;;
    *) GTK_CFLAGS="$GTK_CFLAGS $FLAG";;
  esac;
done;
TMPGTK_LIBS=$GTK_LIBS $PIXBUF_LIBS;
GTK_LIBS=;
for FLAG in $TMPGTK_LIBS; do
  case $FLAG in
    -L*) DIR=`echo $FLAG | $SED "s/-L//"`;
	 if test -d $DIR; then GTK_LIBS="$GTK_LIBS -L$DIR"; fi;;
    *) GTK_LIBS="$GTK_LIBS $FLAG";;
  esac;
done;

dnl Have a special marshall list (available in the source tree of Gtk+ under
dnl gtk/gtkmarshal.list)

AC_MSG_CHECKING(marshal list)
AC_ARG_WITH(mlist, 
	    [  --with-mlist=GTK-SOURCE use special marshall list from GTK+ sources],
	    [MARSHALLDEFS=$withval;
	     AC_MSG_RESULT($withval)],
	    [MARSHALLDEFS=$TOP/gtk/signals/gtkmarshal.list;
	     AC_MSG_RESULT(built-in)])



dnl c2hs Dilemma.
dnl We provide a local copy of Manuel's c2hs which is patched so that is can
dnl process several .chs files at the same invocation. This patch was not
dnl incooperated into the main c2hs version since Manuel wants to do some
dnl other fundamental changes first. The nice thing is that with the built-in
dnl c2hs this whole package can be built without any other libraries.

dnl Check if the user supplied a specific C->Haskell installation or wants to
dnl use the version in the current search path (--with-c2hs-config=yes). 
dnl The default is to use the built-in version.
AC_ARG_WITH(c2hs, 
  [  --with-c2hs=C2HS-CONFIG use an external C->Haskell installation (slower!)],
  [
    case $withval in
      yes) {
	BUILT_IN_C2HS=no;
        AC_PATH_PROG(C2HS_CONFIG, c2hs-config, notfound)
        if test $C2HS_CONFIG = notfound; then
          AC_MSG_ERROR([C->Haskell was not found in current search path.
	          Try compiling with the built-in c2hs by omitting
		  --with-c2hs-config=... when calling ./configure .])
	fi
	} ;;
      no) {
	BUILT_IN_C2HS=yes
	} ;;
      *) {
	BUILT_IN_C2HS=no;
	AC_CHECK_FILE($withval,C2HS_CONFIG=$withval,
          AC_MSG_ERROR([The specified C->Haskell tool was not found.
	          Try compiling with the built-in c2hs by omitting
		  --with-c2hs-config=... when calling ./configure .]))
	} ;;
    esac
  ],[BUILT_IN_C2HS=yes])

dnl The big switch differing between built-in and external c2hs.

AC_MSG_CHECKING([kind of C->Haskell])
if test $BUILT_IN_C2HS = yes; then
  AC_MSG_RESULT([built-in])
  dnl Use the local c2hs.
  C2HS=$TOP/c2hs/c2hs;
  MULTIPLE_CHS=yes;

  dnl These are the settings needed to compile c2hs.
  LEGACY_FFI=no;
  BEGIN_LEGACY_FFI="{- for systems including the Legacy FFI";
  END_LEGACY_FFI="-}";
  BEGIN_NEW_FFI=;
  END_NEW_FFI=;
  BEGIN_NHC="{- NHC does some things differently...";
  END_NHC="-}";
  BEGIN_NOT_NHC=;
  END_NOT_NHC=;

else
  AC_MSG_RESULT([external])
  MULTIPLE_CHS=no;
  dnl Find C->Haskell and check its version.
  dnl Check the version of c2hs
  AC_CACHE_CHECK([c2hs version], c2hs_version, [
    c2hs_version=`$C2HS_CONFIG --version | $SED "s/[\([0-9.]*\) .*]/\1/"`;
  ])
  GTKHS_PROG_CHECK_VERSION($c2hs_version, -lt, 0.8.0,
    AC_MSG_ERROR([You need C->Haskell version 0.8.0 upwards!
      ** Download from \"http://www.cse.unsw.edu.au/~chak/haskell/c2hs/\". **]))
  GTKHS_PROG_CHECK_VERSION($c2hs_version, -lt, 0.10.13,
    AC_MSG_WARN([
      *************************************************************
      * You may run into problems with this version of C->Haskell.*
      * If c2hs stumbles over VARARG macros edit                  * 
      * c2hs/toplevel/C2HSConfig.hs by changing the line          *
      *   cppopts = ""                                            *
      * to                                                        *
      *   cppopts = "-x c"                                        *
      * and recompile and reinstall c2hs. This problem is fixed   *
      * in version 0.10.13 and above.                             *
      *************************************************************]))
  dnl C->Haskell configuration.
  C2HS=`$C2HS_CONFIG --c2hs`;
fi

dnl C2HS_CFLAGS="`$C2HS_CONFIG --cflags`"
dnl AC_MSG_RESULT([Notice: C->Haskell induced compile time options: `$C2HS_CONFIG --cflags`])
dnl LIBS="`$C2HS_CONFIG --libs` $LIBS"
dnl AC_MSG_RESULT([Notice: C->Haskell induced link time options: `$C2HS_CONFIG --libs`])

# Read the version file
VERSION=`cat $TOP/VERSION`

dnl Documentation

BUILDDOCS=yes;

dnl Check for the XSLT translator
FOUNDTRANSLATOR=yes;

AC_PATH_PROG(XSLTTRANS, xsltproc, no-translator)

if test $XSLTTRANS = no-translator; then
  BUILDDOCS=no;
  FOUNDTRANSLATOR=no;
fi

AC_ARG_WITH(catalog,
  [  --with-catalog=catalog  an XML catalog file pointing to DocBook],
  [XML_CATALOG_FILES=$withval;], 
  [if test -z $XML_CATALOG_FILES; then
    case "$target" in
	*freebsd*) XML_CATALOG_FILES=/usr/local/share/xml/catalog;;
	*) XML_CATALOG_FILES=/usr/share/xml/catalog;;
    esac
  fi])
AC_MSG_CHECKING([files XML catalog])
OLDIFS=$IFS;
IFS=:;
FOUNDCATALOG=no;
for CATALOG in $XML_CATALOG_FILES; do
  if test -r $CATALOG; then FOUNDCATALOG=yes; fi;
done;
IFS=$OLDIFS;
if test $FOUNDCATALOG = no; then
  BUILDDOCS=no;
fi;
AC_MSG_RESULT([$FOUNDCATALOG]);

dnl Check for XSLT translation descriptions
FOUNDHTML=yes;
FOUNDFO=yes;
AC_ARG_WITH(html,
  [  --with-html=html.xsl    an XSLT file for generating (X)HTML],
  [XSLTHTML=$withval;], 
  [XSLTHTML=no;])
AC_ARG_WITH(fo,
  [  --with-fo=fo.xsl        an XSLT file for generating formatting objects (FO)],
  [XSLTFO=$withval;], 
  [XSLTFO=no;])
AC_MSG_CHECKING([XSLT translations])
dnl if the user just says --with-html, we still have to search for it
if test $XSLTHTML = yes; then XSLTHTML=no; fi;
if test $XSLTFO = yes; then XSLTFO=no; fi;
XSLTPATH=no-xslt;
OLDIFS=$IFS;
IFS=:;
for DIR in $XML_CATALOG_FILES; do
  XSLTPATH=`dirname $DIR | $SED s/xml/xsl/`;
  if test -r $XSLTPATH/docbook/xhtml/chunk.xsl; then
    if test $XSLTHTML = no; then
      XSLTHTML=$XSLTPATH/docbook/xhtml/chunk.xsl;
    fi;
  fi;
  if test -r $XSLTPATH/docbook/fo/docbook.xsl; then
    if test $XSLTFO = no; then
      XSLTFO=$XSLTPATH/docbook/fo/docbook.xsl;
    fi;
  fi;
done;
IFS=$OLDIFS;
if test ! -r $XSLTHTML; then
  FOUNDHTML=no;
fi;
if test ! -r $XSLTFO; then
  FOUNDFO=no;
fi;
if test $FOUNDHTML = no -a $FOUNDFO = no; then
  BUILDDOCS=no;
fi;
AC_MSG_RESULT([HTML: $FOUNDHTML, FO: $FOUNDFO])
if test $BUILDDOCS = yes; then
  AC_MSG_RESULT([HTML: $XSLTHTML])
  AC_MSG_RESULT([FO: $XSLTFO])
fi;

    
dnl Needed substitution.
AC_SUBST(PWD)
AC_SUBST(TOP)
AC_SUBST(GTK_CFLGAS)
AC_SUBST(GTK_LIBS)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(BUILT_IN_C2HS)
AC_SUBST(MULTIPLE_CHS)
AC_SUBST(FOUR_WORD_CALLBACK)
AC_SUBST(NEW_MODULE_SYSTEM)
AC_SUBST(LOCALPKGCONF)
AC_SUBST(HCFLAGS)
AC_SUBST(C2HS)
AC_SUBST(MARSHALLDEFS)
AC_SUBST(VERSION)
dnl Platform specific flags 
AC_SUBST(DLPREFIX)
AC_SUBST(DLSUFFIX)
AC_SUBST(SLPREFIX)
AC_SUBST(SLSUFFIX)
AC_SUBST(WIN32)
AC_SUBST(PATHSED)
AC_SUBST(HSCFLAGS)
AC_SUBST(C2HSFLAGS)
AC_SUBST(EXTRA_HFILES)
dnl Versionitis
dnl AC_SUBST(GTK_MAJOR_VERSION)
dnl AC_SUBST(GTK_MINOR_VERSION)
dnl AC_SUBST(GTK_MICRO_VERSION)
AC_SUBST(ENABLE_OPENGL)
dnl Documentation
AC_SUBST(BUILDDOCS)
AC_SUBST(XSLTTRANS)
AC_SUBST(XML_CATALOG_FILES)
AC_SUBST(FOUNDHTML)
AC_SUBST(XSLTHTML)
AC_SUBST(FOUNDFO)
AC_SUBST(XSLTFO)
dnl The c2hs part.
AC_SUBST(CPP)
AC_SUBST(LEGACY_FFI)
AC_SUBST(BEGIN_LEGACY_FFI)
AC_SUBST(END_LEGACY_FFI)
AC_SUBST(BEGIN_NEW_FFI)
AC_SUBST(END_NEW_FFI)
AC_SUBST(BEGIN_NHC)
AC_SUBST(END_NHC)
AC_SUBST(BEGIN_NOT_NHC)
AC_SUBST(END_NOT_NHC)

dnl write the results...
AC_OUTPUT([
  gtk2hs.spec
  mk/config.mk
  mk/chsDepend
  c2hs/toplevel/C2HSConfig.hs
],[chmod a+x mk/chsDepend])

dnl ...and chat with the user
echo "**************************************************"
echo "* Configuration completed successfully.          *"
echo "*                                                *"
if test $BUILDDOCS = no; then
  echo "* Warning: The documentation will not be built:  *"
  if test $FOUNDTRANSLATOR = no; then
    echo "*   - the xsltproc translator was not found      *"
  fi;
  if test $FOUNDCATALOG = no; then
    echo "*   - no XML catalog files were found            *"
  fi;
  if test $FOUNDHTML = no; then
    echo "*   - no HTML XSL translation file was found     *"
  fi;
  if test $FOUNDFO = no; then
    echo "*   - no FO XSL translation file was found       *"
  fi;
  echo "*                                                *"
fi;
if test $ENABLE_OPENGL = no; then
  echo "* Warning: OpenGL support is not available:      *"
  if test x$FOUNDGLEXT = xno; then
    echo "*   - the GtkGLExt widget is not installed       *"
  fi;
  if test x$FOUNDHOPENGL = xno; then
    echo "*   - HOpenGL is not installed in the specified  *"
    echo "*     GHC installation                           *"
  fi;
  echo "*                                                *"
fi;
echo "* Now do \"(g)make install\".                      *"
echo "**************************************************"
